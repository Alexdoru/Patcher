buildscript {
    ext.kotlin_version = '1.3.72'
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }

        flatDir dirs: 'libs'
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: 'kotlin'

version = modVersion
group = modGroup
archivesBaseName = modBaseName

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = project.forgeVersion
    runDir = "run"

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = project.mcpVersion
    makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    clientJvmArgs += "-Dfml.coreMods.load=club.sk1er.patcher.tweaker.PatcherTweaker"
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs += ['-Xopt-in=kotlin.RequiresOptIn', '-Xno-param-assertions']
    }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

repositories {
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    provided files("libs/modcore.jar")
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation ("org.jetbrains.kotlin:kotlin-reflect:$kotlin_version") {
        exclude module: "kotlin-stdlib"
    }

    implementation("com.github.Sk1erLLC:HookInjection:b9ac92c31e") {
        exclude module: "koffee"
        exclude module: "kotlin-reflect"
        exclude module: "kotlin-stdlib-jdk8"
        exclude module: "asm-debug-all"
    }

    implementation('com.github.half-cambodian-hacker-man:Koffee:d8cee73') {
        exclude module: 'asm-commons'
        exclude module: 'asm-tree'
        exclude module: 'asm'
    }

    implementation('org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7') {
        exclude module: "kotlin-stdlib-common"
        exclude module: "kotlin-stdlib"
    }
}

task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/java"
    }
}

moveResources.dependsOn processResources
classes.dependsOn moveResources

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info", "LICENSE.txt"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
    dependsOn configurations.compile
    from {
        configurations.embed.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    manifest {
        attributes "FMLCorePlugin": "club.sk1er.patcher.tweaker.PatcherTweaker",
                "ModSide": "CLIENT",
                'FMLAT': 'patcher_at.cfg',
                "FMLCorePluginContainsFMLMod": "Yes, yes it does",
                "Main-Class": "club.sk1er.container.ContainerMessage"
    }
}