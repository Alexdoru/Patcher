// For those who want the bleeding edge
buildscript {
    repositories {
        mavenCentral()
        maven { url "https://files.minecraftforge.net/maven" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url 'https://jitpack.io' }
        maven { url "https://repo.sk1er.club/repository/maven-public" }
    }

    dependencies {
        classpath('com.github.ReplayMod:ForgeGradle:48c4f0c6c7:all')
        classpath('com.github.LXGaming:MixinGradle:0.6-SNAPSHOT')
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url "https://repo.sk1er.club/repository/maven-public" }
    maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
    maven {
        credentials {
            username "$nexus_user"
            password "$nexus_password"
        }
        url "https://repo.sk1er.club/repository/maven-private"
    }
}

apply plugin: 'kotlin'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.replaymod.preprocess'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

version = this.properties.getOrDefault("BUILD_ID", "LOCAL")
group = modGroup
archivesBaseName = modBaseName

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = '1.8.9-11.15.1.2318-1.8.9'
    mappings = "stable_22"
    runDir = "run"
    makeObfSourceJar = false
    clientJvmArgs += ["-Dfml.coreMods.load=club.sk1er.patcher.tweaker.PatcherTweaker", "-Dpatcher.debugBytecode=true", "-Dmixin.debug.verbose=true", "-Dmixin.debug.export=true", "-Dmixin.dumpTargetOnFailure=true"]
    clientRunArgs += ["--tweakClass gg.essential.loader.stage0.EssentialSetupTweaker", "--mixin patcher.mixins.json"]
}

mixin {
    add sourceSets.main, "patcher.mixins.refmap.json"
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

def mcVersionStr = "${(int) (mcVersion / 10000)}.${(int) (mcVersion / 100) % 100}" + (mcVersion % 100 == 0 ? '' : ".${mcVersion % 100}")

dependencies {
    def kotlin_version = "1.5.21"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation("org.jetbrains.kotlin:kotlin-reflect:$kotlin_version") {
        exclude module: "kotlin-stdlib"
    }

    embed "gg.essential:loader-launchwrapper:1.0.2"
    implementation "gg.essential:essential-${mcVersionStr}-forge:1260"

    embed("com.github.Sk1erLLC:HookInjection:b9ac92c31e") {
        exclude module: "koffee"
        exclude module: "kotlin-reflect"
        exclude module: "kotlin-stdlib-jdk8"
        exclude module: "asm-debug-all"
    }

    implementation('com.github.half-cambodian-hacker-man:Koffee:d8cee73') {
        exclude module: 'asm-commons'
        exclude module: 'asm-tree'
        exclude module: 'asm'
    }

    annotationProcessor 'org.spongepowered:mixin:0.7.11-SNAPSHOT'
    // Mixin needs these (and depends on them but for some reason that's not enough. FG, did you do that?)
    annotationProcessor 'com.google.code.gson:gson:2.2.4'
    annotationProcessor 'com.google.guava:guava:21.0'
    annotationProcessor 'org.ow2.asm:asm-tree:6.2'
}

task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/java"
    }
}

moveResources.dependsOn processResources
classes.dependsOn moveResources

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    exclude "META-INF/**"
    copy {
        from("src/main/resources")
        into("build/classes/main")
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

jar {
    dependsOn configurations.embed
    from(configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'club.sk1er.vigilance.example.ExampleMod', 'dummyThing'
        def i = 0
        filesMatching("META-INF/NOTICE*") { name = "$name.${i++}" }
        filesMatching("META-INF/LICENSE*") { name = "$name.${i++}" }
        filesMatching("META-INF/mods.toml") { name = "$name.${i++}" }
        filesMatching("LICENSE*") { name = "$name.${i++}" }
    }

    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    manifest {
        attributes "FMLCorePlugin": "club.sk1er.patcher.tweaker.PatcherTweaker",
                "ModSide": "CLIENT",
                'FMLAT': 'patcher_at.cfg',
                "FMLCorePluginContainsFMLMod": "Yes, yes it does",
                "Main-Class": "club.sk1er.container.ContainerMessage",
                "TweakClass": "gg.essential.loader.stage0.EssentialSetupTweaker",
                "TweakOrder": "0",
                "MixinConfigs": "patcher.mixins.json"
    }
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs += ['-Xopt-in=kotlin.RequiresOptIn', '-Xno-param-assertions']
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = "Patcher"
            def mcVersion
            def (major, minor, patch) = project.name.tokenize('-')[0].tokenize('.')
            mcVersion = "${major}${minor.padLeft(2, '0')}${(patch ?: '').padLeft(2, '0')}" as int
            version = "${this.properties.getOrDefault("branch", "master") != "master" ? this.properties.getOrDefault("branch", "master") + "-" : ""}${project.version}-$mcVersion${this.properties.getOrDefault("IS_CI", false) ? "-SNAPSHOT" : ""}"

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.implementation.allDependencies.each {
                    if (it.group != null && it.name != null && it.name != "forge" && it.version != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }

            artifact jar
        }
    }

    repositories {
        maven {
            name 'nexus'
            url "https://repo.sk1er.club/repository/maven-private-${this.properties.getOrDefault("IS_CI", false) ? "snapshots" : "releases"}/"

            credentials {
                username project.nexus_user
                password project.nexus_password
            }
        }
    }
}
